plugins {
    id "com.android.application"
    id "kotlin-android"
}

android {
    namespace "com.videowall.client"
    compileSdk 34

    defaultConfig {
        applicationId "com.videowall.client"
        minSdk 21
        targetSdk 34
        versionCode 36
        versionName "0.8.0"
        manifestPlaceholders = [ screenOrientation: "landscape" ]
        buildConfigField "String", "WS_URL", '"ws://192.168.12.193:8088/ws"'
    }

    signingConfigs {
        // Use the standard debug keystore so release builds are installable without extra credentials
        release {
            storeFile file("${System.properties['user.home']}/.android/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
            signingConfig signingConfigs.release
        }
        debug {
            debuggable = true
        }
    }

    flavorDimensions += "screen"
    productFlavors {
        left {
            dimension "screen"
            buildConfigField "String", "ROLE", '"left"'
            manifestPlaceholders = [ screenOrientation: "portrait" ]
        }
        center {
            dimension "screen"
            buildConfigField "String", "ROLE", '"center"'
            manifestPlaceholders = [ screenOrientation: "landscape" ]
        }
        right {
            dimension "screen"
            buildConfigField "String", "ROLE", '"right"'
            manifestPlaceholders = [ screenOrientation: "portrait" ]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    // Ensure APK file names carry role, build type, version, and build timestamp to avoid confusion
    def buildTimestamp = new Date().format("yyyyMMdd-HHmmss")
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def flavorPart = variant.flavorName ?: "noflavor"
            def typePart = variant.buildType.name
            outputFileName = "videowall-${flavorPart}-${typePart}-v${variant.versionName}-${buildTimestamp}.apk"
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.10"
    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "com.google.android.material:material:1.11.0"

    implementation "com.google.android.exoplayer:exoplayer:2.19.1"
    implementation "com.squareup.okhttp3:okhttp:4.12.0"
    implementation "org.java-websocket:Java-WebSocket:1.5.4"
}

